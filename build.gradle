group 'com.vtence.mario'
version '0.3.0'

apply plugin: 'java'
sourceCompatibility = 1.8

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'com.bmuschko.nexus'

repositories {
    mavenCentral()
}

def libs = [
        selenium_api           : 'org.seleniumhq.selenium:selenium-api:2.53.0',
        hamcrest               : 'org.hamcrest:java-hamcrest:2.0.0.0',

        junit                  : 'junit:junit:4.12@jar',
        hamcrest_junit         : 'org.hamcrest:hamcrest-junit:2.0.0.0',
        selenium_firefox_driver: 'org.seleniumhq.selenium:selenium-firefox-driver:2.53.0'
]

def providedDependencies = []
ext {
    provided = { providedDependencies << it; it }
}


dependencies {
    compile libs.hamcrest
    compile libs.selenium_api, provided

    testCompile libs.junit
    testCompile libs.hamcrest_junit
    testCompile libs.selenium_firefox_driver
}

compileJava.options.encoding = 'UTF-8'

task 'version' << {
    println version
}

modifyPom {
    project {
        name 'Mario'
        description 'A library for asynchronous testing using WebDriver'
        url 'http://vtence.com/mario'
        packaging 'jar'
        licenses {
            license {
                name 'BSD License'
                url 'https://opensource.org/licenses/BSD-3-Clause'
                distribution 'repo'
            }
        }
        scm {
            url 'https://github.com/testinfected/mario'
            connection 'scm:git:https://github.com/testinfected/mario.git'
            developerConnection 'scm:git@github.com:testinfected/mario.git'
        }
        developers {
            developer {
                id 'testinfected'
                name 'Vincent TencÃ©'
            }
        }

        providedDependencies.each { dep ->
            dependencies.find { it.artifactId == dep.name }.scope = 'provided'
        }

        // no need to publish test dependencies
        dependencies.removeAll { it.scope == "test" }
    }
}

